apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

dependencies {
    compile fileTree(dir: '../libs', include: ['*.jar'])
    compile project(':realmbuilders')
    compile 'com.squareup:javapoet:1.7.0'
    testCompile 'junit:junit:4.12'
    testCompile 'io.realm:realm-annotations:1.2.0'
}

//def publishingArtifactId = 'android-realm-builders'
//def publishingArtifactName = 'android-realm-builders'
//def publishingArtifactVersion = version
//def publishingArtifactGroup = commonArtifactProperties.artifactGroup
//def publishingArtifactDescription = commonArtifactProperties.artifactDescription
//def publishingSiteUrl = commonArtifactProperties.siteUrl
//def publishingGitUrl = commonArtifactProperties.gitUrl

// Artifacts
task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}
task sourcesJar(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

def artifactID = 'android-realm-builders-compiler'

// Maven POM
publishing {
    publications {
        library(MavenPublication) {
            groupId group
            artifactId artifactID
//            version publishingArtifactVersion
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                Node root = asNode()
                root.appendNode('name', artifactID)
                root.appendNode('description', artifactDescription)
                root.appendNode('url', siteUrl)

                def issues = root.appendNode('issueManagement')
                issues.appendNode('system', 'github')
                issues.appendNode('url', siteUrl + '/issues')

                def scm = root.appendNode('scm')
                scm.appendNode('url', siteUrl)
                scm.appendNode('connection', gitUrl)
                //scm.appendNode('developerConnection', commonArtifactProperties.gitConnection)

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')
            }
        }
    }
}

// Bintray release
Properties bintrayProperties = new Properties()
bintrayProperties.load(project.rootProject.file('bintray.properties').newDataInputStream())
bintray {
    // user and key come from the bintray.properties file in project root (not included in git)
    user = bintrayProperties.getProperty('bintray.user')
    key = bintrayProperties.getProperty('bintray.apikey')
    publications = ['library']
    publish = rootProject.hasProperty('snapshot') ? snapshot.toBoolean() : false
    dryRun = false
    pkg {
        repo = 'maven'
        name = artifactID
        desc = artifactDescription
        websiteUrl = siteUrl
        issueTrackerUrl = siteUrl + '/issues'
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        labels = ['android', 'realm']
        publicDownloadNumbers = false
    }
}
