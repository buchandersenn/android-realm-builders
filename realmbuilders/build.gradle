apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

dependencies {
    compile fileTree(dir: '../libs', include: ['*.jar'])
}

// Artifacts
task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}
task sourcesJar(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

def artifactID = 'android-realm-builders'

// Maven POM
publishing {
    publications {
        library(MavenPublication) {
            groupId group
            artifactId artifactID
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                Node root = asNode()
                root.appendNode('name', artifactID)
                root.appendNode('description', artifactDescription)
                root.appendNode('url', siteUrl)

                def issues = root.appendNode('issueManagement')
                issues.appendNode('system', 'github')
                issues.appendNode('url', siteUrl + '/issues')

                def scm = root.appendNode('scm')
                scm.appendNode('url', siteUrl)
                scm.appendNode('connection', gitUrl)
                //scm.appendNode('developerConnection', gitUrl)

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')
            }
        }
    }
}

// Bintray username/password comes from the bintray.properties file in the project root,
// and is not included in git
Properties bintrayProperties = new Properties()
bintrayProperties.load(project.rootProject.file('bintray.properties').newDataInputStream())

// SNAPSHOT configuration
artifactory {
    contextUrl = 'https://oss.jfrog.org'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = bintrayProperties.getProperty('bintray.user')
            password = bintrayProperties.getProperty('bintray.apikey')
            maven = true
        }
        defaults {
            publications('library')
            publishPom = true
            publishIvy = false
        }
    }
}

// Bintray release configuration
bintray {
    user = bintrayProperties.getProperty('bintray.user')
    key = bintrayProperties.getProperty('bintray.apikey')
    publications = ['library']
    publish = true // Auto-publish release
    dryRun = false
    pkg {
        repo = 'maven'
        name = artifactID
        desc = artifactDescription
        websiteUrl = siteUrl
        issueTrackerUrl = siteUrl + '/issues'
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        labels = ['android', 'realm']
        publicDownloadNumbers = false
    }
}
